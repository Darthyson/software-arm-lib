plugins {
    id 'java'
    id 'application'
    id 'jvm-test-suite'
}

group 'org.selfbus'
version '1.20' ///\todo also change in ../README.md and ToolInfo.java (String version)

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = 'org.selfbus.updater.Updater'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.selfbus.updater.Updater'
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    archiveClassifier.set('all')
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        /*
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/LGPL2.1"
        exclude "META-INF/AL2.0"
        exclude "META-INF/versions/9/module-info.class"
         */
    }

    with jar
}

artifacts {
    archives fatJar
}

gradle.projectsEvaluated{
    tasks.withType(JavaCompile){
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-Xlint:deprecation"
        options.compilerArgs << "-Xlint:all"
    }
}

testing {
    //
    // comment avoids warning "suites' cannot be applied to '(groovy.lang.closure)'"
    //
    //noinspection GroovyAssignabilityCheck
    suites {
        test {
            useJUnitJupiter()
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

//
// Repositories are now configured in settings.gradle, so we can include https://jitpack.io.
// We need it to add java-intelhex-parser as an dependency, instead of copy/pasting the source.
//
dependencies {
    // calimero knx bus access library
    implementation 'com.github.calimero:calimero-core:2.6-rc1'

    // calimero serial tx/rx lib for ft1.2 and tpuart support
    implementation 'com.github.calimero:calimero-rxtx:2.6-rc1'

    // find specific directories under linux and windows
    implementation 'net.harawata:appdirs:1.2.2'

    // For search in byte array
    implementation 'com.google.guava:guava:33.2.1-jre'

    // command line option
    implementation 'commons-cli:commons-cli:1.8.0'

    // console and file logging
    implementation 'ch.qos.logback:logback-classic:1.5.6'

    // Console ansi color support
    implementation 'org.fusesource.jansi:jansi:2.4.0'

    // GUI
    implementation files('libs/forms_rt.jar')

    // Intel hex parser
    // commit 9dec823 of 2023/02/27
    // https://github.com/j123b567/java-intelhex-parser/commit/9dec82355697dba028e8aa1832b8b9ee1b66d9e2
    implementation 'com.github.j123b567:java-intelhex-parser:9dec823'
    // To revert this, just delete "implementation 'com.github.j123b567:java-intelhex-parser:9dec823'"
    // and c&p the code of
    // ../cz/jaybee/intelhex to
    // ../software-arm-lib/firmware_updater/updater/source/src/main/java/cz/jaybee/intelhex
}