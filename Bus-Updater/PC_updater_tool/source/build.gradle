plugins {
    id 'java'
    id 'application'
}

group 'org.selfbus'
version '0.62' ///\todo also change in README.md and ToolInfo.java (String version)

sourceCompatibility = 11

jar {
    manifest {
        attributes 'Main-Class': 'org.selfbus.updater.Updater'
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    archiveClassifier.set('all')
    duplicatesStrategy = DuplicatesStrategy.WARN
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    with jar
}

artifacts {
    archives fatJar
}

repositories {
    mavenCentral()
    // jcenter() // since march 2021 readonly
}

gradle.projectsEvaluated{
    tasks.withType(JavaCompile){
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-Xlint:deprecation"
    }
}

/*
compileJava.options*.compilerArgs = [
        '-Xlint:all',
        '-Xlint:-options',
//	'-Werror'
        "--limit-modules", "java.base,java.xml"
]

compileTestJava.options*.compilerArgs = [
        '-Xlint:all',
        '-Xlint:-try',
]
*/


dependencies {
    // calimero knx bus access library
    implementation 'com.github.calimero:calimero-core:2.5'

    // calimero serial tx/rx lib for ft1.2 and tpuart support
    implementation 'com.github.calimero:calimero-rxtx:2.5'

    // find specific directories under linux and windows
    implementation 'net.harawata:appdirs:1.2.1'

    // For search in byte array
    implementation 'com.google.guava:guava:31.0.1-jre'

    // command line option
    implementation 'commons-cli:commons-cli:1.5.0'

    // console and file logging
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha10'

    // unit test
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}
